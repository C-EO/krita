SET(EXTPREFIX_ocio "${EXTPREFIX}" )

set(ext_ocio_PATCH_COMMAND "")

if(APPLE)
    set(ext_ocio_PATCH_COMMAND ${PATCH_COMMAND} -p1 -i ${CMAKE_CURRENT_SOURCE_DIR}/0003-macOS-fixWarnings.patch)
    if(CMAKE_SYSTEM_PROCESSOR STREQUAL "arm64")
        set(SSE_OPTIMIZATIONS_STRING "-DCMAKE_OSX_ARCHITECTURES=arm64")
    endif()
endif()

# OCIO tarball has files with CRLF, this confuses patch on POSIX.
if (WIN32)
    set(ext_ocio_PATCH_COMMAND ${PATCH_COMMAND} -p1 -i ${CMAKE_CURRENT_SOURCE_DIR}/0001-Fix-MinGW-and-MSVC-detection.patch)
endif()

set(ext_ocio_PATCH_COMMAND ${ext_ocio_PATCH_COMMAND}
    COMMAND ${PATCH_COMMAND} -p1 -i ${CMAKE_CURRENT_SOURCE_DIR}/0002-Auto-disable-SSE-if-the-compiler-target-doesn-t-supp.patch
    COMMAND ${PATCH_COMMAND} -p1 -i ${CMAKE_CURRENT_SOURCE_DIR}/0004-Remove-Werror-to-fix-build-on-GCC11.patch
)

ExternalProject_Add(
    ext_ocio
    DOWNLOAD_DIR ${EXTERNALS_DOWNLOAD_DIR}
    URL https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v1.1.1.tar.gz
    URL_MD5 23d8b9ac81599305539a5a8674b94a3d

    INSTALL_DIR ${EXTPREFIX_ocio}
    PATCH_COMMAND ${ext_ocio_PATCH_COMMAND}

    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=${EXTPREFIX_ocio} -DCMAKE_BUILD_TYPE=${GLOBAL_BUILD_TYPE} ${GLOBAL_PROFILE} -DOCIO_BUILD_APPS=OFF -DOCIO_BUILD_TRUELIGHT=OFF -DOCIO_BUILD_NUKE=OFF -DOCIO_BUILD_DOCS=OFF -DOCIO_BUILD_TESTS=OFF -DOCIO_BUILD_PYGLUE=OFF -DOCIO_BUILD_STATIC_JNIGLUE=OFF ${SSE_OPTIMIZATIONS_STRING}

    UPDATE_COMMAND ""
    DEPENDS ext_boost
)
